; Generated NSIS script file (generated by makensitemplate.phtml 0.21)
; by 130.225.167.75 on Sep 25 02 @ 09:39

; NOTE: this .NSI script is designed for NSIS v1.8+
; GetFileTimeLocal "..\Release\avisynth.dll",$WORD1,$WORD2
BrandingText "Avisynth 2 installer."
Name "AviSynth 2.06"
OutFile "AviSynth206.exe"

#EnabledBitmap "on.bmp"
#DisabledBitmap "off.bmp"

; Some default compiler settings (uncomment and change at will):
SetCompress auto ; (can be off or force)
SetDatablockOptimize on ; (can be off)
CRCCheck on ; (can be off)
AutoCloseWindow true ; (can be true for the window go away automatically at end)
; ShowInstDetails hide ; (can be show to have them shown, or nevershow to disable)
; SetDateSave off ; (can be on to have files restored to their orginal date)

LicenseText "You must agree to this license before installing."
LicenseData "gpl.txt"

InstallDir "$PROGRAMFILES\AviSynth2"
InstallDirRegKey HKEY_LOCAL_MACHINE "SOFTWARE\AviSynth" ""
DirShow show ; (make this hide to not let the user change it)
DirText "Select the directory to install documentation in:"


Section "AviSynth Base" ; (default section)
SetOutPath "$SYSDIR"
File "..\release\avisynth.dll"
;RegDll "$SYSDIR\avisynth.dll"

; Write GPL
SetOutPath "$INSTDIR"
File "gpl.txt"


ReadRegStr $0 HKEY_LOCAL_MACHINE "SOFTWARE\AviSynth" "plugindir"
StrCmp "$0" "" No_Plugin_exists Plugin_exists
No_Plugin_exists:
CreateDirectory "$INSTDIR\plugins"
StrCpy $0 "$INSTDIR\plugins"
Plugin_exists:

IfErrors ignore_me    ; Clear errors up till now.
ignore_me:

; add files / whatever that need to be installed here.
WriteRegStr HKEY_LOCAL_MACHINE "SOFTWARE\AviSynth" "plugindir" "$0"
WriteRegStr HKEY_LOCAL_MACHINE "SOFTWARE\AviSynth" "" "$INSTDIR"
WriteRegStr HKEY_LOCAL_MACHINE "Software\Microsoft\Windows\CurrentVersion\Uninstall\AviSynth2" "DisplayName" "AviSynth 2 (remove only)"
WriteRegStr HKEY_LOCAL_MACHINE "Software\Microsoft\Windows\CurrentVersion\Uninstall\AviSynth2" "UninstallString" '"$INSTDIR\uninst.exe"'
WriteRegStr HKEY_CLASSES_ROOT "avifile\Extensions\AVS" "" "{E6D6B700-124D-11D4-86F3-DB80AFD98778}"
WriteRegStr HKEY_CLASSES_ROOT "Media Type\Extensions\.avs" "Source Filter" "{D3588AB0-0781-11CE-B03A-0020AF0BA770}"
WriteRegStr HKEY_CLASSES_ROOT "CLSID\{E6D6B700-124D-11D4-86F3-DB80AFD98778}" "" "Avisynth"
WriteRegStr HKEY_CLASSES_ROOT "CLSID\{E6D6B700-124D-11D4-86F3-DB80AFD98778}\InProcServer32" "" "avisynth.dll"
WriteRegStr HKEY_CLASSES_ROOT "CLSID\{E6D6B700-124D-11D4-86F3-DB80AFD98778}\InProcServer32" "ThreadingModel" "Apartment"

IfErrors reg_not_ok
goto reg_ok
reg_not_ok:
MessageBox MB_OK "Could not register filter properly - Be sure to have administrator rights, and try again."
reg_ok:


; Create links
CreateDirectory "$SMPROGRAMS\AviSynth 2"
CreateShortCut "$SMPROGRAMS\AviSynth 2\Plugin Directory.lnk" "$0"
CreateShortCut "$SMPROGRAMS\AviSynth 2\Uninstall AviSynth.lnk" "$INSTDIR\uninst.exe"
CreateShortCut "$SMPROGRAMS\AviSynth 2\License.lnk" "$INSTDIR\gpl.txt"
; write out uninstaller
WriteUninstaller "$INSTDIR\uninst.exe"

SectionEnd ; end of default section

Section "Documentation (optional)"
  SectionIn 1
  CreateDirectory "$INSTDIR\docs"
  SetOutPath "$INSTDIR\docs"
	File "..\docs\*.html"
  SetOutPath "$INSTDIR\docs\filters"
	File "..\docs\filters\*.html"
  CreateShortCut "$SMPROGRAMS\AviSynth 2\Avisynth Documentation.lnk" "$INSTDIR\docs\index.html"
  CreateShortCut "$SMPROGRAMS\AviSynth 2\Avisynth Online.lnk" "http://avisynth.org"
SectionEnd

; begin uninstall settings/section
UninstallText "This will remove Avisynth from your system"

Section Uninstall
; add delete commands to delete whatever files/registry keys/etc you installed here.
Delete "$INSTDIR\uninst.exe"
DeleteRegKey HKEY_LOCAL_MACHINE "SOFTWARE\AviSynth" 
DeleteRegKey HKEY_LOCAL_MACHINE "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\AviSynth 2"
DeleteRegKey HKEY_CLASSES_ROOT "avifile\Extensions\AVS" 
DeleteRegKey HKEY_CLASSES_ROOT "Media Type\Extensions\.avs" 
DeleteRegKey HKEY_CLASSES_ROOT "CLSID\{E6D6B700-124D-11D4-86F3-DB80AFD98778}" 
DeleteRegKey HKEY_CLASSES_ROOT "CLSID\{E6D6B700-124D-11D4-86F3-DB80AFD98778}\InProcServer32"
DeleteRegKey HKEY_CLASSES_ROOT "CLSID\{E6D6B700-124D-11D4-86F3-DB80AFD98778}\InProcServer32"
;UnRegDLL "$SYSDIR\avisynth.dll"
Delete "$SYSDIR\avisynth.dll"
Delete "$INSTDIR\gpl.txt"
Delete "$INSTDIR\docs\filters\*.*"
Delete "$INSTDIR\docs\*.*"
Delete "$SMPROGRAMS\AviSynth 2\*.*"

RMDir "$INSTDIR\docs\filters"
RMDir "$INSTDIR\docs"
RMDir "$INSTDIR\plugins"
RMDir "$INSTDIR"
RMDir "$SMPROGRAMS\AviSynth 2"
SectionEnd ; end of uninstall section

; eof
